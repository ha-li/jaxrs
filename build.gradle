allprojects {
   apply plugin: 'idea'

   ext {
      projectIds = [
              group  : 'com.gecko',
              name   : "$project.name",
              version: '0.1'
      ]
   }
}

subprojects {
   apply plugin: 'java'
   apply from: "$rootDir/gradle/jacoco.gradle"

   repositories {
      mavenCentral ()
   }
   
   dependencies {
      testCompile 'junit:junit:4.12'
   }

   task sourcesJar (type: Jar) {
      classifier 'sources'
      from sourceSets.main.allSource
   }

   artifacts {
      archives sourcesJar
   }

   configurations.all {
      resolutionStrategy {
         failOnVersionConflict()
         eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'com.fasterxml.jackson.core') {
               details.useVersion "2.5.4"
            }
         }
      }
   }
}

task wrapper (type: Wrapper) {
   gradleVersion = '3.2'
}

